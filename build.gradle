import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath "com.netflix.nebula:gradle-ospackage-plugin:8.5.1"
        classpath "com.palantir.gradle.gitversion:gradle-git-version:0.12.3"
    }
}

plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'uk.co.ractf'
version = '0.0.2'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.0.202103091610-r'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.palantir.git-version'

mainClassName = 'uk.co.ractf.yakrazor.YakrazorApplication'

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': name
        attributes 'Implementation-Version': version
        attributes 'Implementation-Vendor-Id': group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'yakrazor.jar'
}

buildRpm {
    packageName = 'yakrazor'
    def details = versionDetails(prefix: 'rc@')
    release = details.lastTag
    version = rootProject.version
    arch = 'X86_64'
    os = 'LINUX'
    license = 'AGPL'
    url = 'https://github.com/ractf/yakrazor'
    vendor = 'RACTF'
    packageDescription = 'The RACTF test runner'
    maintainer = 'RACTF <admins@ractf.co.uk>'
    user = 'root'
    group = 'root'

    requires('systemd')
    requires('java-11-openjdk')
    requires('docker-ce')
    requires('docker-compose')

    from(shadowJar.outputs.files) {
        into '/opt/yakrazor'
        createDirectoryEntry = true
    }
}
